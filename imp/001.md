---
layout: page
---

I implemented the language more or less as Martin originally presented it. It works well enough to port some [simple feature engineering](https://www.kaggle.com/mrisdal/exploring-survival-on-the-titanic/code) from R. My goal is to be able to actually try out the language in practice and find out:

* can we assign a consistent semantics to this language (yes)
* is it possible to lower the whole language to first-order (mostly)
* what is it like writing actual code (mixed)

Contents:

* [Semantics](#semantics)
  * [Core](#core)
  * [Booleans](#booleans)
  * [Everything](#everything)
  * [Equality](#equality)
  * [Natives](#natives)
  * [Reduce](#reduce)
  * [Higher-order sets](#higher-order-sets)
* [Evaluation](#evaluation)
  * [Parsing](#parsing)
  * [Inlining](#inlining)
  * [Lowering](#lowering)
  * [Bounding](#bounding)
  * [Interpreter](#interpreter)
* [UX](#ux)
* [Next](#next)
  * [Better lowering](#better-lowering)
  * [Higher-order puzzles](#higher-order-puzzles)
  * [Interpreter -> rewriter](#interpreter---rewriter)
  
## Semantics

The semantics mostly follow Martin's initial presentation. I diverged in places where the semantics weren't clear or where I thought they could be simplified. 

For the sake of quick prototyping I implemented this as Julia macro and piggybacked on the Julia parser, so the syntax is very different from Martin's in places. This is purely a pragmatic choice, not a strong opinion on syntax. 

Been working in Julia 0.7 - might need some effort to make it work on 0.6.

```
jamie@machine:~$ julia-nightly
               _
   _       _ _(_)_     |  A fresh approach to technical computing
  (_)     | (_) (_)    |  Documentation: https://docs.julialang.org
   _ _   _| |_  __ _   |  Type "?help" for help.
  | | | | | | |/ _` |  |
  | | |_| | | | (_| |  |  Version 0.7.0-DEV.5052 (2018-05-10 03:02 UTC)
 _/ |\__'_|_|_|\__'_|  |  Commit b5815c8a19 (4 days old master)
|__/                   |  x86_64-pc-linux-gnu

julia> using Imp
```

### Core 

This part is all identical to Martin's initial presentation, so I'll move quickly.

Expression oriented. The value of an expression is always a set of tuples of scalars. 

(I followed Julia's approach of defining semantics for a very broad language and then defining a smaller subset which can be efficiently compiled. In particular, the semantics allow for mixed-arity sets and for ill-typed expressions (eg `let x = 1; x(2,3)`) and the interpreter supports these, even though the lowering pass currently doesn't.)

Integer, float and string constants:

``` julia
julia> @imp! 1
(1,)

julia> @imp! 4.2
(4.2,)

julia> @imp! "Hello, world!"
("Hello, world!",)
```

Global variables:

``` julia
julia> @imp! x = 1
(1,)

julia> @imp! x
(1,)
```

Local variables:

``` julia
julia> @imp! x
(1,)

julia> @imp! let x = 2; x; end
(2,)

julia> @imp! x
(1,)
```

Function declaration and application:

``` julia
julia> @imp! let always_one = x -> 1; always_one(2); end
(1,)
```

Global functions:

``` julia
julia> @lib always_one = x -> 1

julia> @imp! always_one(2)
(1,)
```

(The `@lib` is required for silly plumbing reasons - globals are always materialized and `always_one` is infinite. If you try to materialize an infinite set you will get a `KeyError`:

``` julia
julia> @imp! always_one = x -> 1
┌ Warning: Cannot support Imp.Var[@imp(:_1)] with Imp.Expr[@imp(:(_2 == 1))]
└ @ Imp Imp.jl:930
ERROR: KeyError: key @imp(:everything) not found
```

This will be fixed.)

Composition:

``` julia
julia> @imp! x.always_one
(1,)
```

Tuples:

``` julia
julia> @imp! (1,2)
(1, 2)

julia> @imp! (1,(2,3))
(1, 2, 3)

julia> @imp! (x, (2,3))
(1, 2, 3)
```

### Booleans

Martin's initial presentation featured scalar booleans, as well as an implicit conversion where eg the set `{1, 2, 3}` could also be treated as a function `1 -> true, 2 -> true, 3 -> true, _ -> false` and vice versa. It was left open when exactly this conversion occurs, but it would need to look something like this: 

``` julia
julia> @imp! alpha
("a", 1)
("b", 2)
("c", 3)

julia> @imp! alpha("a")
(1,)

julia> @imp! alpha("a", 1)
(true,) # <-- implicit boolean

julia> @imp! alpha("b")
(2,)

julia> @imp! alpha("d")
(false,) # <-- implicit boolean 

julia> @imp! alpha("a", 2)
(false,) # <-- implicit boolean
```

I proposed that the difficulty with this conversion was caused by treating booleans as scalars. If we simply follow the rules for function application we get:

``` julia
julia> @imp! alpha
("a", 1)
("b", 2)
("c", 3)

julia> @imp! alpha("a")
(1,)

julia> @imp! alpha("a", 1)
()

julia> @imp! alpha("b")
(2,)

julia> @imp! alpha("d")

julia> @imp! alpha("a", 2)

```

If we denote the empty set as false and the set containing a single empty tuple as true, this is exactly the result we want.

``` julia
julia> @imp! true
()

julia> @imp! false

```

This also works when defining functions:

``` julia
julia> @imp! x -> false

julia> @imp! a -> (a == 1)
(1,)

julia> @imp! a -> alpha(a, 1)
("a",)

julia> @imp! a -> alpha(a, 1 | 2)
("a",)
("b",)
```

We can lift boolean operations to the set level. `|` is union, `&` is intersection:

``` julia
julia> @imp! true | false
()

julia> @imp! false | false

julia> @imp! true & true
()

julia> @imp! true & false

```

As a bonus, the combination of tuples and union gives us a way to write set literals:

``` julia
julia> @imp! alpha = ("a", 1) | ("b", 2) | ("c", 3)
("a", 1)
("b", 2)
("c", 3)
```

This way of handling booleans removes the need for a distinction between formulae (which have boolean value) and expressions (which have set value). Everything is an expression.

If necessary, we can easily convert scalar values into set booleans:

``` julia
julia> @imp! is_evil = ("alice", "no") | ("bob", "no") | ("eve", "yes") | ("dave", "maybe")
("alice", "no")
("bob", "no")
("dave", "maybe")
("eve", "yes")

julia> @imp! is_evil("alice")
("no",)

julia> @imp! is_evil("alice", "yes")

julia> @imp! is_evil("eve", "yes")
()

julia> @imp! is_evil("dave", "yes")

julia> @imp! is_evil("dave", "yes" | "maybe")
()
```

To convert set booleans into scalar values we introduce `if`:

``` julia
julia> @imp! if true "yes" else "no" end
("yes",)

julia> @imp! if false "yes" else "no" end
("no",)
```

### Everything

We can define `exists` as being false for the empty set and true for every other set. 

``` julia
julia> @imp! exists(a -> alpha(a, 1))
()

julia> @imp! exists(a -> alpha(a, 4))

```

Similarly, `!` is true for the empty set and false for every other set. 

``` julia
julia> @imp! !(a -> alpha(a, 1))

julia> @imp! !(a -> alpha(a, 4))
()
```

`a => b` (a implies b) can be desugared to `b | !(a)`.

Defining `forall` is a little trickier, because we want the input to be a single set but we are used to thinking of `forall` as having an explicit domain, like "for all x such that f(x) is true, g(x) is true".

We would write that as `forall(x -> f(x) => g(x))`. We know we want this to be true only when `f(x) => g(x)` is true for all `x`. So `forall(x -> true)` must be true.

`x -> true` is an infinite set which contains every possible scalar value. Call this set `everything` and define `forall(p)` to be true iff `p == everything`. (Later this gets lowered via double negation into something that we can actually execute.)

``` julia
julia> @imp! forall(a -> alpha(a) => alpha(a, 1))

julia> @imp! forall(a -> alpha(a) => alpha(a, 1 | 2 | 3))
()
```

Conveniently, if we define `_ = everything` then this also provides the correct semantics for anonymous variables.

``` julia
julia> @imp! alpha(_)
(1,)
(2,)
(3,)

julia> @imp! a -> alpha(a, _)
("a",)
("b",)
("c",)
```

### Equality

We can use function application to test scalar equality:

``` julia
julia> @imp! x = 1
(1,)

julia> @imp! x(1)
()

julia> @imp! x(2)

```

But there is no way in the language to test set equality:

``` julia
julia> @imp! x = 1 | 2
(1,)
(2,)

julia> @imp! x(1)
()

julia> @imp! x(1 | 2)
()

julia> @imp! x(1 | 2 | 3)
()
```

So I defined `==` as set equality rather than pointwise scalar equality:

``` julia
julia> @imp! x == 1

julia> @imp! x == (1 | 2)
()

julia> @imp! x == (1 | 2 | 3)

```

It's tricky to derive bounds though, so many expressions involving `==` will not work. 

``` julia
julia> @imp! a -> if alpha(a,1); "one" end
("a", "one")

julia> @imp! a -> if alpha(a) == 1; "one" end
┌ Warning: Cannot support Imp.Var[@imp(:_1)] with Imp.Expr[@imp(:((_3->begin
│               alpha(_1, _3)
│           end) == (_4->begin
│               _4 == 1
│           end))), @imp(:(_2 == "one"))]
└ @ Imp Imp.jl:930
ERROR: KeyError: key @imp(:everything) not found
```

I *think* just requires a bit more work in the bounding pass, rather than being a fundamental problem. TBD. 

### Natives

Some sets (eg `+`) are infinite but we can specify the relationship between the columns (eg if `a` and `b` are finite then `a + b` is finite and we can materialize the result). These are represented by `Native`:

```julia
struct Native <: Expr
    f::Function
    in_types::NTuple{N, Type} where N
    out_types::NTuple{N, Type} where N
end
```

This represents an infinite set of arity `length(in_types) + length(out_types)`, where if the first `length(in_types)` values are given we can calculate a finite number of matching tuples values by calling `f`. 

There is currently no syntax for natives, but they can be inserted into the global scope:

```julia
julia> Imp.global_lib[Imp.Var(:+)] = Imp.Native(+, (Int64, Int64), (Int64,))
@imp(:({Native(+, (Int64, Int64), (Int64,))}))

julia> @imp! 1 + 2
(3,)
```

The reason for specifying types and not just the number of values is that the function may throw an error if called on the wrong types, and so far I have no idea how to approach error handling in this language. It's not sufficient to just pass the error up because an expression like `integer(a) & integer(b) & (a + b == c)` is well-defined but may still end up calling `+` on a string during evaluation, depending on the evaluation order. The only behavior I can think of that is consistent with treating `+` as a set is to not return any results:

```julia
julia> @imp! add = (1,1,2) | (1,2,3) # etc...
(1, 1, 2)
(1, 2, 3)

julia> @imp! add(1,2)
(3,)

julia> @imp! add("a","b")

julia> @imp! "a" + "b"

```

Generic functions pose an additional problem. We can certainly specify a looser type bound, `Number` rather than `Int64`:

```julia
julia> Imp.global_lib[Imp.Var(:+)] = Imp.Native(+, (Number, Number), (Number,))
@imp(:({Native(+, (Number, Number), (Number,))}))

julia> @imp! 0.1 + 1
(1.1,)
```

The difficulty here is that Imp's type inference is not yet able to specialize this into more concrete types eg `1 + 2` now has the inferred type `Number`. Fixing this is not entirely straightforward, because the type of the Imp set is not always the same as the type of the Julia function. For example:

```julia
julia> Imp.global_lib[Imp.Var(:even)] = Imp.Native(iseven, (Integer,), ())
@imp(:({Native(iseven, (Integer,), ())}))

julia> @imp! even(1)

julia> @imp! even(2)
()

julia> @imp! even("a")

julia> @imp! even(alpha(_))
()
```

The Julia function `iseven` has type `f(::Integer)::Bool` but the Imp set `even` has type `(Integer,)`, because the boolean is implicitly represented by set membership rather than by a scalar value.

Another example:

```julia
julia> imp_split(string, regex) = collect(enumerate(map(String, split(string, Regex(regex)))))

julia> Imp.global_lib[Imp.Var(:split)] = Imp.Native(imp_split, (String, String), (Int64, String))
@imp(:({Native(imp_split, (String, String), (Int64, String))}))

julia> @imp! split("a b c", " ")
(1, "a")
(2, "b")
(3, "c")

julia> @imp! split("a b c", " ", 2)
("b",)

julia> @imp! i -> split("a b c", " ", i, "b")
(2,)
```

The type of the Julia function `imp_split` is `imp_split(::String, ::String)::Vector{Tuple{Int64, String}}` but the Imp set `split` has type `(String, String, Int64, String)`. 

Given an arbitrary Julia function which may have an infinite number of concrete methods, each of which can have a different return type and even produce a different arity set, it's not clear how to automatically assign it a single type. At least some of the work is going to have to be done manually in the interface.

The runtime behavior is currently governed by:

``` julia
return!(result::Set, returned::Union{Vector, Set}) = foreach(returned -> return!(result, returned), returned)
return!(result::Set, returned::Tuple) = push!(result, returned)
return!(result::Set, returned::Bool) = returned && push!(result, ())
return!(result::Set, returned::Nothing) = nothing
return!(result::Set, returned) = push!(result, (returned,))
```

Another missing facet is functions which have multiple functional dependencies eg given `a + b == c` we can use `a` and `b` to calculate `c`, but we can also use `b` and `c` to calculate `a`. I'm not sure whether this is best expressed in the native interface itself or whether there is value in a language-level means to declare that two expressions are equivalent and can be used interchangeably. 

### Reduce

Reduce can be defined loosely by `reduce(f, init, a | b | c)` => `f(f(f(init, a), b), c)`. 

```julia
julia> @imp! reduce(+, 0, 1 | 2 | 3)
(6,)

julia> @imp! op = (0, 1 | 2 | 3, 0)
(0, 1, 0)
(0, 2, 0)
(0, 3, 0)

julia> @imp! reduce(op, 0, 1 | 2 | 3)
(0,)
```

The rows of the reduced set are sorted in lexicographic order and only the last value in each row is used. 

Even if `f` is not total or not a function, the result is still well-defined. Efficiently implementing this is another question though, so currently the interpreter will bail if it encounters anything interesting:

```julia
julia> @imp! op = (0, 1, "a" | "b")
(0, 1, "a")
(0, 1, "b")

julia> @imp! reduce(op, 0, 1 | 2 | 3)
ERROR: AssertionError: length(result) == 1
```

Reducing with native functions or materialized sets works well. Reducing with infinite sets *should* work, but the variable ordering is broken at the moment:

```julia
@imp! ((a, b) -> a + b + b)(1,2)
ERROR: AssertionError: raw_op.query_vars[1:2] == raw_op.yield_vars[1:2]
```

Fixing this requires some refactoring.

### Higher-order sets

We need higher-order sets (sets that contain sets) to express things like `sum`. The syntax for these mirrors the syntax for first-order sets:

```julia
julia> @lib sum = {xs} -> reduce(+, 0, xs)

julia> @imp! sum{1 | 2 | 3}
(6,)

julia> @lib restrict = {f, domain} -> (x -> if domain(x) f(x) end)

julia> @imp! restrict{x -> x + 1, 1 | 2 | 3}
(1, 2)
(2, 3)
(3, 4)

julia> @lib subset = {a, b} -> forall(x -> a(x) => b(x))

julia> @imp! subset{1, 1 | 2}
()

julia> @imp! subset{1 | 2, 1 | 2}
()

julia> @imp! subset{1 | 2, 2}

```

These are problematic semantically, because unrestricted higher-order sets allow us to write paradoxical expressions:

```julia
julia> @lib rp = {x} -> !x{x}

julia> @imp! rp{rp}
# ?
```

There are various existing ways of dealing with this:

* Require an explicit domain for set comprehensions. (This is the [standard solution](https://en.wikipedia.org/wiki/Axiom_schema_of_specification) in everyday set theory).
* Stratify sets. 1st-order sets may contain scalars, 2nd-order sets may contain 1st-order sets and scalars etc. This is roughly equivalent to an explicit domain where the domain is something like 'all sets of at most 3rd order'. (This is roughly what Principia Mathematica and [later simplifications](https://en.wikipedia.org/wiki/Stratification_(mathematics)#In_set_theory) do.)
* Disallow negation in the body of set comprehensions. (As in [positive set theory](https://en.wikipedia.org/wiki/Positive_set_theory).)

Currently, the interpreter evaluates `rp{rp}` to `StackOverflow`, which is at least not incorrect. This is the immediate inlining strategy proposed by Martin, which amounts to requiring strict stratification. We might be able to find relaxations of this, similarly to stratification in datalog. 

We also have to figure out how it interacts with normal datalog stratification too eg `fixpoint = {f, x} -> let fx = f(x); if fx == x; x else fixpoint{f, fx} end; end` is stratifiable only for certain `f`.

## Evaluation

I started with a naive interpreter that can handle the entire language apart from unapplied functions (`x -> ...`). Functions are difficult to handle naively because they are effectively a comprehension over the infinite set `everything`, even though the actual result may be finite.

One way to sidestep this is to work in a smaller universe where, say, `everything = 0 | 1 | 2 | "a" | "b" | "c"` and then interpret functions directly as a comprehension over this small set. This doesn't help us write real programs, but it allows writing tests.

The compiler is then a series of passes that gradually rewrites functions and other difficult abstractions into simpler expressions. The output of each pass is still an Imp expression and can be run in the interpreter, so we can test whether the pass changed the result of an expression. This makes it much easier to locate bugs. 

The passes are grouped into parsing, inlining, lowering and bounding.

### Parsing

[Expr](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L29-L79)

[map_expr](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L95-L108)

I generate a function `map_expr(f, expr)` which applies `f` to all immediate `Expr` children of `expr`. This is the backbone of all the various rewrites.

[Parser](https://github.com/jamii/imp/blob/master/src/Imp.jl#L110-L162)

Since `@imp!` is a macro, the input to the parser is a Julia Expr. I use [MacroTools](https://github.com/MikeInnes/MacroTools.jl) to pattern match on the Expr. The parser is just a big case statement with various patterns, and calls itself recursively. There is no backtracking and no context so simply throwing an exception has been good enough for error reporting so far.

```julia
julia> imp(:(let one = x -> 1; one(3); end), passes=[Imp.PARSE])
@imp(:(let one = (x->begin
                  1
              end)
      one(3)
  end))
```

[Scoping](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/)

A separate pass after parsing assigns scope ids to each variable, so that variables with the same name in different scopes are not `==`.

### Inlining

[Let](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L243-L248)

Local definitions `let x = ...; ...; end` are inlined without any processing. (This is not ideal - in more complex expressions it might be faster to completely lower the let before inlining it.)

```julia
julia> imp(:(let inc = x -> x + 1; inc(3); end), passes=Imp.PARSE:Imp.INLINE)
@imp(:(((x->begin
          ({Native(+, (Number, Number), (Number,))})(x, 1)
      end))(3)))
```

[HigherApply/Abstract](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L250-L265)

Higher-order functions which are directly applied (after inlining `let`) are expanded. This is sufficient for all the examples, but it means that the *only* thing you can do with a higher-order function is apply it or pass it to another higher-order function. You can't eg union two higher-order functions. I find this sharp divide between first-order and higher-order worrying - if possible I would much prefer a model like PLINQ where there is no distinction.

```julia
julia> imp(:(let sum = {x} -> reduce(+, 0, x); sum{1 | 2 | 3}; end), passes=Imp.PARSE:Imp.INLINE)
@imp(:(reduce({Native(+, (Number, Number), (Number,))}, 0, (1 | 2) | 3)))
```

### Lowering

The goal of lowering is to make everything look like datalog, which we already know how to compile efficiently.

[Arity inference](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L500-L559)

This is a subset of full type inference, but it's faster and simpler by itself. Since we allow multiple arity sets, the result of inference is a set of possible arities. If the set is empty then we can safely replace the expression with `false`. If the set has more than one arity, we refuse to lower the expression since we can't desugar tuple and compose. 

```julia
julia> expr = imp(:(x -> false), passes=Imp.PARSE:Imp.INLINE)

julia> Imp.infer_arity(Imp.Env{Set{Int64}}(), expr)[expr]
Set(Int64[])

julia> expr = imp(:(let inc = x -> x + 1; inc(3); end), passes=Imp.PARSE:Imp.INLINE)

julia> Imp.infer_arity(Imp.Env{Set{Int64}}(), expr)[expr]
Set([1])

julia> expr = imp(:( 1 | ("a", "b") ), passes=Imp.PARSE:Imp.INLINE)

julia> Imp.infer_arity(Imp.Env{Set{Int64}}(), expr)[expr]
Set([2, 1])
```

[Desugaring](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L578-L601)

Tuple (`(x, y)`) and compose (`x.y`) can be desugared to simpler expressions once their arity is known.

```
julia> imp(:( ("a", "b") ), passes=Imp.PARSE:Imp.LOWER)
@imp(:((_3, _4)->begin
          (true & (_3 == "a")) & (_4 == "b")
      end))

julia> @imp! x = (1, 2)

julia> @imp! y = (2, 3, 4)

julia> imp(:( x.y ), passes=Imp.PARSE:Imp.LOWER)
@imp(:((_6, _7, _8)->begin
          exists((_9->begin
                      x(_6, _9) & y(_9, _7, _8)
                  end))
      end))
```

[Simple application](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L611-L726)

This is the interesting part. We want every application to be in the form `f(a,b,c)` where `f` is a global and `a`, `b`, `c` etc are scalar variables introduced by an earlier function declaration `(a,b,c) -> ...`.

Given an expression `e`, we infer the arity and wrap it in a matching function and application: `(_1, _2, _3) -> e(_1, _2, _3)`. Then we recursively simplify the right-hand side until we reach the bottom of the expression. There are a lot of cases to consider, but each one is simple to reason about in isolation and the top-down simplification makes it easy to reason about invariants/termination. 

A side effect of this is that everything inside the outer function declaration now has boolean type, so we can further desugar some operations. `if c t else f end` becomes `(c & t) | (!c & f)` and `forall(p)` becomes `!(x -> !p(x))`.

Some expressions (`!`, `reduce`, `==`) aren't further simplified. Instead we simply start the process again on their arguments. They can be treated much like native functions thereafter - some opaque block of code that takes some scalar inputs and returns a set.

[Negation](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L728-L742)

The bounding pass which comes next can't see inside negation, so pushing negation downwards through conjunction/disjunction makes it possible to derive tighter bounds.

[Union](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L753-L762)

Similarly, deriving bounds for union/disjunction is more complicated so we push it up out of functions. 

At this point we get something close to DNF, except that at the leaves we may have `!`/`reduce`/`==` whose arguments may contain a new expression in the same form. 

```julia
julia> imp(:(x -> let y = f(x) & g(x); if string(y) y end; end), passes=Imp.PARSE:Imp.LOWER)
@imp(:(((_1, _2)->begin
              exists((()->begin
                              exists((_3->begin
                                          ({Native(getfield(Main, Symbol("##3#4"))(), (String,), ())})(_3) & (f(_1, _3) & g(_1, _3))
                                      end))
                          end)) & (f(_1, _2) & g(_1, _2))
          end) | ((_1, _2)->begin
              (()->begin
                          !((_4->begin
                                      ({Native(getfield(Main, Symbol("##3#4"))(), (String,), ())})(_4) & (f(_1, _4) & g(_1, _4))
                                  end))
                      end) & false
          end)))

```

### Bounding

The goal of bounding is to split each function into an upper bound on the function variable and a remainder that needs to be check against each element of the bound. This is essentially the same transformation as GenericJoin, but the supported language here is richer.

```julia
julia> imp(:(x -> let y = f(x) & g(x); if string(y) y end; end), passes=Imp.PARSE:Imp.BOUND)
@imp(:(for _3 = (f[2])() & (g[2])() # <-- bound
          for _1 = (((f[2, 1])(_3) & (g[2, 1])(_3)) & (f[1])()) & (g[1])() # <-- bound
              for _2 = (f[1, 2])(_1) & (g[1, 2])(_1) # <-- bound
                  if &(((({Native(getfield(Main, Symbol("##3#4"))(), (String,), ())})(_3))[])()) # <-- remainder
                      return (_1, _2)
                  end
              end
          end
      end | false))
```

This pass is where most of the known bugs live. It needs cleaning up.

[ConjuctiveQuery and Permute](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L786-L843)

Two new Expr types:

`ConjuctiveQuery` is basically a datalog rule. This can be expressed in the language already, but it's easier to manipulate when it's all gathered in one node. 

`Permute` projects and/or reorders the columns of a set. This can be expressed with functions (eg `(a, b) -> x(b, a)`) but it's one of the primitive operations to which functions will be reduced, so it's useful to have as an explicit node. 

[Collecting ConjunctiveQuery](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L940-L979)

This is pretty boring. Just compacting a big chunk of Exprs into one.

[Variable ordering](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L894-L938)

Naive topological sort. Tries to stick to the order in which variables appeared in the source code, since in past work that has been a reasonable heuristic in the absence of a proper query optimizer.

[Computing bounds/remainders](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L981-L1022)

This is conceptually simple, but fiddly in practice. 

Suppose we have pick the variable ordering `_1, _2, _3` and we are trying to bound `_2`. If we see the clause `f(_2, _1, _3)`, we can derive the bound `_2 in f[2,1](_1)` and remember that we still have to check `f(_2, _1, _3)`. If we see the clause `f(_2, _1)`, we can derive the bound `_2 in f[2,1](_1)` and that satisfies the clause entirely, so we can now throw it away.

Unfortunately we don't just have simple function applications in here, we also have `Native`, `everything` and `reduce`/`!`/`==`. Dealing with this all cases is error-prone, which probably means that this pass is trying to do too much work at once.

[Indexes](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L1068-L1089)

The bounds are expressed in terms of intersection and projection. With sorted index structures these are efficient operations, but I'm still using Julia's `Set`. As a temporary fix, I build `Dict` indexes for each bound to speed up the interpreter.

### Interpreter

[Core interpreter](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L274-L326)

[Value interpreter](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L330-L425)

The interpreter is split into core code which operates on any abstract set representation and value code which operates specifically on literal sets of tuples of scalars. 

``` julia
julia> imp(:(let inc = x -> x + 1; inc(3); end))
Set(Tuple{Int64}[(4,)])
```

[Type interpreter](https://github.com/jamii/imp/blob/5e2a69641a07bd00102d7f27ac03b8eeae4980bb/src/Imp.jl#L429-L496)

Overloading the methods in the latter allows running the interpreter on sets of tuples of types instead, providing dataflow type inference. 

This is only capable of dealing with a finite set of types though. Allowing potentially infinite types (eg tuples or value creation) might require something more sophisticated.

```julia
julia> expr = imp(:(let inc = x -> x + 1; inc(3); end), passes=Imp.PARSE:Imp.INLINE)
@imp(:(((x->begin
          ({Native(+, (Int64, Int64), (Int64,))})(x, 1)
      end))(3)))

julia> Imp.infer_types(Imp.global_env, Set{Type}([Int64, Float64, String]), expr)[expr]
Set(Tuple{Vararg{Type,N}} where N[(Int64,)])
```

## UX

Early days yet. So far, only [small tests](https://github.com/jamii/imp/blob/master/test/basic.jl#L131-L337) and some [simple feature engineering](https://github.com/jamii/imp/blob/master/test/titanic.jl).

Automatic flattening is quite nice. Avoids boilerplate data-structure traversal eg:

```julia
@imp rare_title = "Dona" | "Lady" | "the Countess" |"Capt" | "Col" | "Don" | "Dr" | "Major" | "Rev" | "Sir" | "Jonkheer"
@imp canonical_title = ("Mlle", "Miss") | ("Ms", "Miss") | ("Mme", "Mrs") | (rare_title, "Rare Title")
@imp title = p -> xor{p.title.canonical_title, p.title}
```

The mixture of compose and apply allows writing OOPy chains of functions nicely:

```julia
@imp family = p -> p.surname.join("_").join(p.fsize.int_to_string)
```

Compose and reduce are limited to operating on single scalars. Can't handle eg reduce over pairs of integers. At least not directly - can use keys eg `reduce((init, i) -> op(init, f(i)), init, i -> exists(f(i)))`.

Really easy to accidentally create infinite sets. Harder to figure out why. Eg:

```julia
@imp fsized = p -> begin
    if p.fsize <= 1
        "singleton"
    elseif p.fsize <= 4
        "small"
    else
        "large"
    end
end
```

Ideally I would want to print out the finite part of this with a `p -> "large"` row at the end. Tricky to implement.

Similar for aggregation:

```julia
# infinite
@imp title_vs_sex = (t, s) -> count{p -> title(p, t) & sex(p, s)}

# finite
@imp title_vs_sex = (t, s) -> if title(_, t) & sex(_, s)
    count{p -> title(p, t) & sex(p, s)}
end
```

Often use aggregation to quickly summarize information, so it needs to be printable. 

Eval-ability, for want of a better word, is non-local. It's difficult to tell from a chain of higher-order definitions whether the result will be successfully bounded, even if it is finite. This is less the case for datalog, where we have a local rule (every variable positively bound) to determine whether something will compile. 

## Next

### Better lowering

`==` and `reduce` are still buggy and cause much of the complexity in the bounding pass. I need to give more thought as to how to lower/bound them, and any similar set-level primitives that we want to add.

### Higher-order puzzles

There isn't any way yet to abstract over arity. Tuple/compose are not expressible in the language itself, which means any other similar operators we discover will also have to be implemented as primitives. Martin suggested a variable-arity function (something like `(xs...) -> ...`) but I don't know how to interpret it. Perhaps by always requiring a domain?

We want to keep things first order, so we implicitly flatten everything (in apply, compose etc). But then we need to pretend to have higher-order things for the sake of abstraction, so we have a separate syntax for not flattening things (`f{x}`). But we also need a separate kind of function declaration for not flattening sets (`{x} -> ...`). And maybe a third kind of function declaration for not flattening tuples (`(xs...) -> ...`). Starting to feel like we're missing something. 

What are the fundamental operations that allow us to express all of this flattening in the language, rather than just adding a new feature every time we encounter a new usecase?

### Interpreter -> rewriter

Assumed so far that the interpreter/repl will evaluate everything to a materialized set. But it turns out in practice that it's common to write expressions that are composed of an interesting finite part and an boring infinite part (eg aggregates/functions without a domain, functions with default values). We want to be able to separate these and print the finite part. 

We also have various kinds of sets (eg native functions, higher-order functions) which we can't represent directly as simple data-structures, and some (eg mixed-arity sets, mixed-type sets) which are complicated to represent efficiently. 

It looks like the core team is going down the path of making different AbstractRelation subtypes and implementing some common interface. I'm worried that this will end up duplicating a lot of logic between the compiler and the runtime, since both now have to know what happens if we eg join two native functions together, or join a native function with a mixed-arity set etc. 

```julia
julia> @lib my_add = (+) | (1,1,42)
@imp(:({Native(+, (Int64, Int64), (Int64,))} | (1, 1, 42)))

julia> @imp! my_add(1,1)
(2,)
(42,)
```

We can already represent and manipulate all of these sets as `Imp.Expr`s, so I want to try replacing the interpreter with a rewriting system that reduces expressions to some normal form. 

I can already do this:

```julia
julia> @imp! nums = 1 | 2 | 3
(1,)
(2,)
(3,)

julia> @lib f = x -> if nums(x) x else 0 end

julia> @imp! f(1)
(1,)

julia> @imp! f(4)
(0,)
```

I'd like to be able to do this:

```julia
julia> @imp! nums = 1 | 2 | 3
(1,)
(2,)
(3,) 

julia> @imp! f = x -> if nums(x) x else 0 end
(1, 1)
(2, 2)
(3, 3)
x -> 0
```
